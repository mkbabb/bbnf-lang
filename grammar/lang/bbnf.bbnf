identifier = /[_a-zA-Z][_a-zA-Z0-9-]*/ ;

literal = "\"" , /(\\.|[^"\\])*/  , "\""
        | "'"  , /(\\.|[^'\\])*/  , "'"
        | "`"  , /(\\.|[^`\\])*/  , "`" ;

big_comment = ( "/*" , /[^\*]*/ , "*/" ) ?w ;
comment = ( "//" , /.*/ ) ?w ;

regex = "/" , /(\\.|[^\/])+/ , "/" ;

lhs = identifier ;

term = "Îµ"
     | identifier
     | literal 
     | regex
     | "(" , rhs ?w , ")" 
     | "[" , rhs ?w , "]"
     | "{" , rhs ?w , "}" ;


modifier = "?w" | "?" | "*" | "+" ;
factor = big_comment ? , term ?w , modifier ? , big_comment ? ;

binary_operators = "<<" | ">>" | "-" ;

binary_factor = factor , ( binary_operators ?w , factor ) * ;

concatenation = ( binary_factor ?w , "," ? ) + ;
alternation = ( concatenation ?w , "|" ? ) + ;

rhs = alternation ;

rule = lhs, "=" ?w, rhs ?w, ( ";" | "." ) ;

import_path = "\"" , /(\\.|[^"\\])*/ , "\"" ;
import_items = "{" ?w , identifier , ( "," ?w , identifier ) * , "}" ;
import_directive = "@import" ?w , (
      import_items ?w , "from" ?w , import_path
    | import_path
) , ( ";" | "." ) ? ;

recover_directive = "@recover" ?w , identifier ?w , rhs ?w , ( ";" | "." ) ? ;

pretty_hint = identifier ;
pretty_directive = "@pretty" ?w , identifier ?w , (pretty_hint+) ?w , ( ";" | "." ) ? ;

grammar = ( comment ? , ( import_directive | recover_directive | pretty_directive | rule ) ?w , comment ? ) * ;

@pretty grammar block ;
@pretty rule group ;
@pretty alternation group ;